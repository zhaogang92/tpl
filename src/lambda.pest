KEYWORDS = _{
    "if" | "then" | "else" | "lambda" | "Bool" | "true" | "false" | "Top"
}

var = @{
    !(KEYWORDS)
    ~ ASCII_ALPHA+
}

TRUE = { "true" }
FALSE = { "false" }
ZERO = { "0" }

wrap_term = _{ term | "(" ~ term ~ ")" }

pred = { "pred" ~ wrap_term }
succ = { "succ" ~ wrap_term }
iszero = { "iszero" ~ wrap_term }

record_item = { var ~ "=" ~ term }
record = { "{" ~ (record_item ~ ",")* ~ record_item ~ "}" }

proj = { (record | var) ~ "." ~ var}

basic_ty = { "Bool" | "Top" }

lambda_ty = { basic_ty ~ "->" ~ basic_ty }

record_item_ty = { var ~ ":" ~ ty }
record_ty = { "{" ~ (record_item_ty ~ ",")* ~ record_item_ty ~ "}" }

ty = _{ lambda_ty | basic_ty | record_ty}

if_expr = { "if" ~ term ~ "then" ~ term ~ "else" ~ term }

abst = { "lambda" ~ var ~ ":" ~ ty ~ "." ~ wrap_term }

app_template = _{ var | "(" ~ term ~ ")" }

app = { app_template ~ wrap_term }

term = _{ TRUE | FALSE | ZERO | iszero | pred | succ | proj | record | if_expr | abst | app | var}

stmt = { term ~ ";" }

program = _{ SOI ~ stmt+ ~ EOI }


WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }